import org.apache.tools.ant.taskdefs.condition.Os

def modulesList = ['DefectTracker', 'RequirementManager', 'PostBoard']
def modulesDir = './modules'

apply plugin: 'application'

mainClassName = "edu.wpi.cs.wpisuitetng.janeway.Janeway"
//applicationDefaultJvmArgs = ["-Dgreeting.language=en"]

modulesList.each {
    evaluationDependsOn(":${it}")
}

dependencies{
	compile 'com.lowagie:itext:2.1.5'
	compile 'org.swinglabs.swingx:swingx-all:1.6.4'
	compile 'org.jfree:jfreechart:1.0.14'
	compile 'org.jfree:jcommon:1.0.17'
	compile 'com.miglayout:miglayout-swing:4.1' //Used to be 4.0, will this cause issues?
	
    //Core projects
	compile project(':JanewayAPI')
    compile project(':WPISuite-Interfaces')
    compile project(':Network')
	
	//Modules
    modulesList.each {
	    runtime project(":${it}")
        }
}

eclipse{
	project{
		if (Os.isFamily(Os.FAMILY_WINDOWS)) {
			buildCommand 'org.eclipse.ui.externaltools.ExternalToolBuilder', LaunchConfigHandle: '<project>/.externalToolBuilders/Gradle - Prepare Modules - Windows.launch'
		} else {
			buildCommand 'org.eclipse.ui.externaltools.ExternalToolBuilder', LaunchConfigHandle: '<project>/.externalToolBuilders/Gradle - Prepare Modules - Unix.launch'
		}
	}
}

task modules(type: Copy) {
    dependsOn jar
    
    modulesList.each{
        from(project(":${it}").libsDir) {
            include '*.jar'
        }
        into modulesDir
    }
}
